plugins {
    // https://github.com/nemerosa/versioning
    id 'net.nemerosa.versioning' version '3.1.0'
}

ext {
    testingCommonGradle = "https://raw.githubusercontent.com/interlok-testing/testing_common/refs/heads/master/common.gradle?timestamp=${new Date().getTime()}"
//  testingCommonGradle = "../testing_common/common.gradle"
    includeWar='true'
    jrubyVersion = '9.4.12.0'
}

allprojects {
    apply from: "${testingCommonGradle}"
    // specify the version of the configuration.
    version=versioning.info.full
}


dependencies {
    testImplementation ("com.adaptris:interlok-jruby:$interlokVersion") { changing=true }
    interlokRuntime ("com.adaptris:interlok-jruby:$interlokVersion") { changing=true }

    interlokJavadocs group: "com.adaptris", name: "interlok-jruby", version: "$interlokVersion", classifier: "javadoc", changing: true, transitive: false

}

task configureJRuby {
    doLast {
        def jrubyZipFile = "jruby-dist-" + jrubyVersion + "-bin.zip"
        def jrubyUrl = "https://repo1.maven.org/maven2/org/jruby/jruby-dist/" + jrubyVersion + "/" + jrubyZipFile
        def downloadDest = project.buildDir.getCanonicalPath() + "/" + jrubyZipFile
        def jrubyBinDir =  project.buildDir.getCanonicalPath() + "/jruby-" + jrubyVersion + "/bin"
        ant.get(src: jrubyUrl, dest: downloadDest, usetimestamp: true)
        ant.unzip(src: downloadDest, dest: project.buildDir, overwrite:true)
        ant.exec(executable: "cmd", osfamily: "windows", dir: jrubyBinDir, failonerror: true) {
            arg(value: "/c")
            arg(value: "jruby.bat")
            arg(value: "-Sgem")
            arg(value: "install")
            arg(value: "jsonpath")
        }
        ant.exec(executable: "bash", osfamily: "unix", dir: jrubyBinDir, failonerror: true) {
            arg(value: "jruby")
            arg(value: "-Sgem")
            arg(value: "install")
            arg(value: "jsonpath")
        }
    }
}

processTestResources.dependsOn configureJRuby
installDist.dependsOn configureJRuby
functionalTest.dependsOn configureJRuby